expr        : Keyword:var IDENTIFIER ASSIGNMENTOP expr
            | comp-expr ((("&&" | "||") | (Keyword:and | Keyword:or))comp-expr)*

comp-expr   : Keyword:not comp-expr
            | arithm-expr ((RELATIONALOP) arithm-expr)*

arithm-expr : term ((PLUS|MINUS) term)*

term        : factor ((MUL|DIV) factor)*

factor      : (PLUS|MINUS) factor
            | power

power       : call (POW factor)*

call        : atom (LEFTPARENTHESIS (expr (COMMA expr)*) RIGHTPARENTHESIS)?

atom        : IDENTIFIER|INT|FLOAT
            | LEFTPARENTHESIS expr RIGHTPARENTHESIS
            | if-expr
            | for-expr
            | while-expr
            | func-def

if-expr     : Keyword:if expr Keyword:then expr
              (Keyword:elif expr Keyword:then expr)*
              (Keyword:else expr)?

for-expr    : Keyword:for IDENTIFIER ASSIGNMENTOP expr Keyword:to expr
              (Keyword:step expr)? Keyword:then expr

while-expr  : Keyword:while expr Keyword:then expr  

func-def    : Keyword:def IDENTIFIER? LEFTPARENTHESIS 
              (IDENTIFIER (COMMA IDENTIFIER)*)? RIGHTPARENTHESIS
              ARROW expr